/*
 * The conference class represents a collection of talks around the same theme.
 * During the event there can be multiple conferences, some of them even happening
 * at the same time.
 *
 * A conference may happen only during some of the days of the event. Additionaly,
 * you can't have the same conference in multiple stages.
 */
class Conference
types
  public ConfName = seq1 of char;
instance variables
  public Name: ConfName;
  public Stage: Stage`StageName;
  public Start: Date;
  public End: Date;
operations
  public Conference: Stage`StageName * ConfName * Date * Date ==> Conference
  Conference(stage, name, startDate, endDate) == (
  	Stage := stage;
    Name := name;
    Start := startDate;
    End := endDate;
    return self
  )
  pre (
  	startDate.IsEarlierThan(endDate)
  	and endDate.IsLaterThan(startDate)
  );
  
  pure public IsFree: Date * Time * Time * WebSummit`WSTalks ==> bool
	IsFree(date, fromTime, toTime, talks) == (
		for all talk in set rng talks do
			if ( 
				talk.Conference = Name 
				and date.Equals(talk.Date) 
				and Time`DoOverlap(fromTime, toTime, talk.Start, talk.End) 
			) 
			then return false;
		return true;
	);
end Conference