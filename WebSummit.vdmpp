class WebSummit
types
	public WSAccount = seq of Transaction;
	public WSWaitingList = set of User`UserName;
	public WSAttendees = map User`UserName to User;
	public WSTicketType = <Company> | <Attendee> | <Volunteer>;
	public WSTicketStock = map WSTicketType to nat;
	public WSTicketPrice = map WSTicketType to nat;
	public WSVenues = map Venue`VenueName to Venue;
	public WSStages = map Stage`StageName to Stage;
	public WSConferences = map Conference`ConfName to Conference;
	public WSTalks = map Talk`TalkTitle to Talk;
	public WSWorkshops = map Workshop`WorkshopName to Workshop;
	public WSNews = map News`NewsTitle to News;
instance variables
	private Account : WSAccount := [];
	public WaitingList : WSWaitingList := {};
	public Attendees : WSAttendees := { |-> };
	private TicketStock : WSTicketStock;
	private TicketPrice : WSTicketPrice;
	public Conferences : WSConferences := { |-> };
	public Workshops : WSWorkshops := { |-> };
	public Venues : WSVenues := { |-> };
	public Stages : WSStages := { |-> };
	public Talks : WSTalks := { |-> };
	public News : WSNews := { |-> };
operations
	-- Constructor
	public WebSummit: WSTicketStock * WSTicketPrice ==> WebSummit
	WebSummit(tickets, price) == (
		TicketStock := tickets;
		TicketPrice := price;
		return self
	);

	-- Sell a ticket 
	public SellTicket: User`UserName * WSTicketType ==> ()
	SellTicket(name, ticket) == (
		TicketStock(ticket) := TicketStock(ticket) - 1;
		Account := [new Transaction(TicketPrice(ticket))] ^ Account;
		Attendees := Attendees ++ { name |-> new User(name, ticket)};
	)
	pre not ticket = <Volunteer> and TicketStock(ticket) > 0
	post (
		card dom Attendees >= card dom Attendees~ 
		and TicketStock(ticket) = TicketStock~(ticket) - 1 
		and len Account = len Account~ + 1
	);

	-- Retrieve the account balance
	public GetAccountBalance: () ==> int
	GetAccountBalance() == (
		return new Utils().SumSeq([t.Amount | t in seq Account]);
	);
	
	-- Register an attendee
	public RegisterAttendee: User`UserName ==> ()
	RegisterAttendee(name) == (
		Attendees(name).State := <Registered>
	)
	pre Attendees(name).State = <Unregistered>
	post Attendees(name).State = <Registered>;
	
	-- Add a potential volunteer to the waiting list
	public AddToWaitingList: User`UserName ==> ()
	AddToWaitingList(name) == (
		 WaitingList := WaitingList union { name }
	)
	pre name not in set dom Attendees
	post card WaitingList >= card WaitingList~;
	
	-- Accept a volunteer from the waiting list
	public AcceptVolunteer: User`UserName ==> ()
	AcceptVolunteer(name) == (
		Attendees := Attendees ++ { name |-> new User(name, <Volunteer>)}
	)
	pre name in set WaitingList and name not in set dom Attendees
	post name in set dom Attendees;
	
	-- Get the total number of ticket holders (volunteers included)
	public GetNumberOfAttendees: () ==> nat
	GetNumberOfAttendees() == ( 
		return card dom Attendees;
	)
	post RESULT = card dom Attendees;
	
	-- Get the actual total number of attendees (volunteers included)
	public GetNumberOfRegisteredAttendees: () ==> nat
	GetNumberOfRegisteredAttendees() == (
		return card { user | user in set rng Attendees & user.State = <Registered> };
	)
	post RESULT = card { user | user in set rng Attendees & user.State = <Registered>};
	
	-- Schedule a venue for the event
	public ScheduleVenue: Venue`VenueName * Date * Date * nat ==> ()
	ScheduleVenue(name, startDate, endDate, rent) == (
		Account := [new Transaction(-rent)] ^ Account;
		Venues := Venues ++ { name |-> new Venue(name, startDate, endDate) };
	)
	post name in set dom Venues and card dom Venues >= card dom Venues~;
	
	-- Add a stage to a venue 
	public AddStage: Venue`VenueName * Stage`StageName ==> ()
	AddStage(venue, name) == (
		Stages := Stages ++ { name |-> new Stage(venue, name, Venues(venue).Start, Venues(venue).End) };
	)
	pre venue in set dom Venues and name not in set dom Stages
	post name in set dom Stages and card dom Stages > card dom Stages~;
	
	-- Schedule a conference 
	public ScheduleConference: Stage`StageName * Conference`ConfName * Date * Date ==> ()
	ScheduleConference(stage, conf, startDate, endDate) == (
		Conferences := Conferences ++ { conf |-> new Conference(stage, conf, startDate, endDate) }
	)
	pre (
		stage in set dom Stages
		and startDate.IsLaterThan(Stages(stage).Start)
		and endDate.IsEarlierThan(Stages(stage).End)
		and Stages(stage).IsFree(startDate, endDate, Conferences, Workshops)
	)
	post conf in set dom Conferences;
	
	-- Schedule a talk
	public ScheduleTalk: Conference`ConfName * Talk`TalkTitle * User`UserName * Date * Time * Time ==> ()
	ScheduleTalk(conf, title, speaker, date, startTime, endTime) == (
		Talks := Talks ++ { title |-> new Talk(title, conf, speaker, date, startTime, endTime) };
	)
	pre (
		conf in set dom Conferences
		and speaker in set dom Attendees
		and date.IsLaterThan(Conferences(conf).Start)
		and date.IsEarlierThan(Conferences(conf).End)
		and Conferences(conf).IsFree(date, startTime, endTime, Talks)
	)
	post (
		title in set dom Talks
	);
	
	-- Retrieve all the talks from a specific day
	public GetTalksOfDay: Date ==> set of Talk
	GetTalksOfDay(date) == (
		return { talk | talk in set rng Talks & talk.Date.Equals(date) };
	)
	post Talks = Talks~;
	
	-- Retrieve all the talks happening at a specific hour of a day
	public GetTalksAtTime: Date * Time ==> set of Talk
	GetTalksAtTime(date, time) == (
		return { talk | talk in set rng Talks & talk.Date.Equals(date) and time.IsLaterThan(talk.Start) and time.IsEarlierThan(talk.End) }
	)
	post Talks = Talks~;
	
	-- Retrieve all the talks given by a speaker
	public GetTalksOfSpeaker: User`UserName ==> set of Talk
	GetTalksOfSpeaker(speaker) == (
		return { talk | talk in set rng Talks & talk.Speaker = speaker };
	)
	post Talks = Talks~;
	
	-- Retrieve all the talks from a specific conference for a specific day
	public GetConferenceTalksOfDay: Conference`ConfName * Date ==> set of Talk
	GetConferenceTalksOfDay(conference, date) == (
		return { talk | talk in set rng Talks & talk.Conference = conference and talk.Date.Equals(date) }
	)
	post Talks = Talks~;
	
	-- Retrieve all the conferences of a day
	public GetConferencesOfDay: Date ==> set of Conference
	GetConferencesOfDay(date) == (
		return { conf | conf in set rng Conferences & date.IsLaterThan(conf.Start) and date.IsEarlierThan(conf.End) }
	)
	post Conferences = Conferences~;
	
	-- Retrieve stage where conference is taking place
	public GetConferenceStage: Conference`ConfName ==> Stage
	GetConferenceStage(conf) == (
		return Stages(Conferences(conf).Stage);
	)
	pre conf in set dom Conferences;
	
	-- Schedule a workshop
	public ScheduleWorkshop: Workshop`WorkshopName * Workshop`WorkshopSubject * Stage`StageName * Workshop`WorkshopCompany * Date * Time * Time * nat ==> ()
	ScheduleWorkshop(name, subject, stage, company, date, startTime, endTime, vacancies) == (
 		Workshops := Workshops ++ { name |-> new Workshop(name, subject, stage, company, date, startTime, endTime, vacancies) }
	)
	pre (
	 	name not in set dom Workshops
	 	and stage in set dom Stages
	 	and date.IsLaterThan(Stages(stage).Start)
	 	and date.IsEarlierThan(Stages(stage).End)
	 	and Stages(stage).IsFree(date, startTime, endTime, Conferences, Workshops)
	)
	post name in set dom Workshops and card dom Workshops > card dom Workshops~;
	
	--Register attendee to one workshop
	public RegisterAttendeeToWorkshop: User`UserName * Workshop`WorkshopName ==> ()
	RegisterAttendeeToWorkshop(user, workshop) == (
		Workshops(workshop).Attendees := Workshops(workshop).Attendees union { user }
	)
	pre (
	 	card Workshops(workshop).Attendees < Workshops(workshop).Vacancies
	 	and user not in set Workshops(workshop).Attendees
	 	and user in set dom Attendees
	 	and Attendees(user).State = <Registered>
	)
	post (
	  user in set Workshops(workshop).Attendees
	  and card Workshops(workshop).Attendees = card Workshops~(workshop).Attendees + 1 
	);
	
	-- Retrieve all workshops
	public GetAllWorkshops: () ==> set of Workshop
	GetAllWorkshops() == (
	  return rng Workshops
	)
	post (
		Workshops = Workshops~
		and RESULT = rng Workshops
	);
	
	-- Retrieve all workshops at a given day
	public GetWorkshopsOfDay: Date ==> set of Workshop`WorkshopName
	GetWorkshopsOfDay(date) == (
		return { workshop | workshop in set dom Workshops & date.Equals(Workshops(workshop).Date) }
	)
	post (
		Workshops = Workshops~
		and RESULT = { workshop | workshop in set dom Workshops & date.Equals(Workshops(workshop).Date) }
	);
	
	-- Retrive all workshops at a given time
	public GetWorkshopsAtTime: Time ==> set of Workshop`WorkshopName
	GetWorkshopsAtTime(time) == (
		return { workshop | workshop in set dom Workshops & time.IsLaterThan(Workshops(workshop).Start) and time.IsEarlierThan(Workshops(workshop).End) }
	)
	post (
		Workshops = Workshops~
		and RESULT = { workshop | workshop in set dom Workshops & time.IsLaterThan(Workshops(workshop).Start) and time.IsEarlierThan(Workshops(workshop).End) }
	);
	
	-- Retrive all attendees of a given workshop
	public GetAttendeesOfWorkshop: Workshop`WorkshopName ==> set of User`UserName
	GetAttendeesOfWorkshop(workshop) == (
	   return Workshops(workshop).Attendees
	)
	pre workshop in set dom Workshops
	post (
	 	Workshops = Workshops~
   	and RESULT = Workshops(workshop).Attendees
	);
	
	-- Retrieve stage where workshop is taking place
	public GetStageOfWorkshop: Workshop`WorkshopName ==> Venue`VenueName
	GetStageOfWorkshop(workshop) == (
	   return Workshops(workshop).Stage
	)
	pre workshop in set dom Workshops
  post (
   	Workshops = Workshops~
   	and RESULT = Workshops(workshop).Stage
  );
	
	-- Retrieve subject of workshop
	public GetSubjectOfWorkshop: Workshop`WorkshopName ==> Workshop`WorkshopSubject
	GetSubjectOfWorkshop(workshop) == (
	   return Workshops(workshop).Subject
	)
	pre workshop in set dom Workshops
  post (
  	Workshops = Workshops~
  	and RESULT = Workshops(workshop).Subject
  );
	
	-- Retrieve company giving a specific workshop
	public GetCompanyOfWorkshop: Workshop`WorkshopName ==> Workshop`WorkshopCompany
  GetCompanyOfWorkshop(workshop) == (
     return Workshops(workshop).Company
  )
  pre workshop in set dom Workshops
  post (
   Workshops = Workshops~
   and RESULT = Workshops(workshop).Company
  );
	
	-- Publish a news arcticle about the event
	public PublishNewsArcticle: User`UserName * News`NewsTitle * News`NewsContent ==> ()
	PublishNewsArcticle(author, title, content) == (
		News := News ++ { title |-> new News(author, title, content) }
	)
	pre (
		author in set dom Attendees
		and title not in set dom News
	)
	post title in set dom News and card dom News > card dom News~;
	
	-- Publish a news arcticle about a conference
	public PublishNewsArcticleAboutConf: User`UserName * News`NewsTitle * News`NewsContent * Conference`ConfName ==> ()
	PublishNewsArcticleAboutConf(author, title, content, conf) == (
		News := News ++ { title |-> new News(author, title, content, conf) } 
	)
	pre (
		author in set dom Attendees
		and conf in set dom Conferences
		and title not in set dom News
	)
	post title in set dom News and card dom News > card dom News~;
	
	-- Retrieve all news arcticles 
	public GetAllNews: () ==> set of News
	GetAllNews() == (
		return rng News;
	)
	post (
	 News = News~
	 and RESULT = rng News
	);
	
	-- Retrieve all news arcticles about a specific conference
	public GetNewsAboutConf: Conference`ConfName ==> set of News
	GetNewsAboutConf(conf) == (
		return { news | news in set rng News & news.Conference = conf }; 
	)
	post (
		News = News~
		and RESULT = { news | news in set rng News & news.Conference = conf }
	);
	
	-- Retrieve all news arcticles written by a specific author
	public GetNewsByAuthor: User`UserName ==> set of News
	GetNewsByAuthor(author) == (
		return { news | news in set rng News & news.Author = author };
	)
	post (
		News = News~
		and RESULT = { news | news in set rng News & news.Author = author }
	);
	
	-- Retrieve all the workshops taking place in a stage
	public GetWorkshopsInStage: Stage`StageName ==> set of Workshop
	GetWorkshopsInStage(stage) == (
		return { workshop | workshop in set rng Workshops & workshop.Stage = stage };
	)
	post (
		Workshops = Workshops~
		and RESULT = { workshop | workshop in set rng Workshops & workshop.Stage = stage }
	);
	
	-- Retrieve all the conferences taking place in a stage
	public GetConferencesInStage: Stage`StageName ==> set of Conference
	GetConferencesInStage(stage) == (
		return { conf | conf in set rng Conferences & conf.Stage = stage };
	)
	post (
		Conferences = Conferences~
		and RESULT = { conf | conf in set rng Conferences & conf.Stage = stage }
	);
	
	-- Retrieve all the workshops taking place in a stage in a given day
	public GetWorkshopsInStageOfDay: Stage`StageName * Date ==> set of Workshop
	GetWorkshopsInStageOfDay(stage, date) == (
		return { workshop | workshop in set rng Workshops & workshop.Stage = stage and workshop.Date = date };
	)
	post (
		Workshops = Workshops~
		and RESULT = { workshop | workshop in set rng Workshops & workshop.Stage = stage and workshop.Date = date }
	);
	
	-- Retrieve all the conferences taking place in a stage in a given day
	public GetConferencesInStageOfDay: Stage`StageName * Date ==> set of Conference
	GetConferencesInStageOfDay(stage, date) == (
		return { conf | conf in set rng Conferences & conf.Stage = stage and date.IsLaterThan(conf.Start) and date.IsEarlierThan(conf.End) };
	)
	post (
		Conferences = Conferences~
		and RESULT = { conf | conf in set rng Conferences & conf.Stage = stage and date.IsLaterThan(conf.Start) and date.IsEarlierThan(conf.End) }
	);
	
	-- Retrieve the venue of a stage
	public GetVenueOfStage: Stage`StageName ==> Venue
	GetVenueOfStage(stage) == (
		return Venues(Stages(stage).Venue)
	)
	post (
		Stages = Stages~
		and RESULT = Venues(Stages(stage).Venue)
	);
end WebSummit